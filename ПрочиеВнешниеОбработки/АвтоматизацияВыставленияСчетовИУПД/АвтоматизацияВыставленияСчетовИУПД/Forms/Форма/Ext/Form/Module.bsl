#Область ЧтениеExcel

&НаСервере
Процедура ЧтениеExcel_через_ТЗ(АдресДанных, РасширениеФайла)

    ПутьКфайлу = ПолучитьИмяВременногоФайла(РасширениеФайла);

    Данные = ПолучитьИзВременногоХранилища(АдресДанных);
    Данные.Записать(ПутьКфайлу);

    ТаблДок = Новый ТабличныйДокумент;

    Попытка
        ТаблДок.Прочитать(ПутьКфайлу);
    Исключение
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = "Не удалось прочитать файл Excel, по причине: " + ОписаниеОшибки();
        Сообщение.Сообщить();
        Возврат;
    КонецПопытки;  
    // {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 

    ЭтоРелизОрдер = (ВидУслуги = "Оформление релиз-ордера");
    ЭтоДокСбор = (ВидУслуги = "Документационный сбор в порту назначения");
    ЭтоУслугаИмпорт = (ВидУслуги = "Услуги по международной перевозке грузов (импорт)");
    ЭтоУслугаЭкспорт = (ВидУслуги = "Услуги по международной перевозке грузов (экспорт)");
   
    Если ВидУслуги = "Оформление релиз-ордера" 
        ИЛИ ВидУслуги = "Документационный сбор в порту назначения" Тогда 
        
        ТЗПервичка = Новый ТаблицаЗначений;
        
        Если ВидУслуги = "Оформление релиз-ордера" Тогда
            ТЗПервичка.Колонки.Добавить("B_L", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("Container", Новый ОписаниеТипов("Строка")); 
            ТЗПервичка.Колонки.Добавить("CTR_type", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("Vessel", Новый ОписаниеТипов("Строка"));  
            ТЗПервичка.Колонки.Добавить("Рейс", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("POL", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("POD", Новый ОписаниеТипов("Строка"));  
            ТЗПервичка.Колонки.Добавить("Экспедитор", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("Договор_экспедитора", Новый ОписаниеТипов("Строка")); 
            ТЗПервичка.Колонки.Добавить("Релиз", Новый ОписаниеТипов("Строка"));  
            ТЗПервичка.Колонки.Добавить("Ставка_релиз_ордер", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("НДС", Новый ОписаниеТипов("Строка"));
        ИначеЕсли ВидУслуги = "Документационный сбор в порту назначения" Тогда
            ТЗПервичка.Колонки.Добавить("B_L", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("Container", Новый ОписаниеТипов("Строка")); 
            ТЗПервичка.Колонки.Добавить("CTR_type", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("Vessel", Новый ОписаниеТипов("Строка"));  
            ТЗПервичка.Колонки.Добавить("Рейс", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("POL", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("POD", Новый ОписаниеТипов("Строка"));  
            ТЗПервичка.Колонки.Добавить("Экспедитор", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("Договор_экспедитора", Новый ОписаниеТипов("Строка")); 
            ТЗПервичка.Колонки.Добавить("Релиз", Новый ОписаниеТипов("Строка"));  
            ТЗПервичка.Колонки.Добавить("Ставка_док_сбор", Новый ОписаниеТипов("Строка"));
            ТЗПервичка.Колонки.Добавить("НДС", Новый ОписаниеТипов("Строка"));
        КонецЕсли;
        
        НомерСтроки = 3;
        СчетчикДобавленных = 0;
        СчетчикПропущенных = 0;
        
        Пока Истина Цикл
            Попытка
                ОбластьЯчейкиB_L = ТаблДок.Область("R" + НомерСтроки + "C1");
                ОбластьЯчейкиContainer = ТаблДок.Область("R" + НомерСтроки + "C2");
                
                ТекстB_L = ОбластьЯчейкиB_L.Текст;
                ТекстContainer = ОбластьЯчейкиContainer.Текст;
                
                Если ПустаяСтрока(ТекстB_L) И ПустаяСтрока(ТекстContainer) Тогда
                    Прервать; 
                КонецЕсли;
                
                ТекстНДС = ТаблДок.Область("R" + НомерСтроки + "C12").Текст;
                ТекстСтавки = "";
                
                Если ВидУслуги = "Оформление релиз-ордера" Тогда 
                    ТекстСтавки = ТаблДок.Область("R" + НомерСтроки + "C11").Текст;
                ИначеЕсли ВидУслуги = "Документационный сбор в порту назначения" Тогда
                    ТекстСтавки = ТаблДок.Область("R" + НомерСтроки + "C11").Текст;
                КонецЕсли;
                
                НДСКорректный = Найти(ТекстНДС, "%") > 0 И ЭтоЧислоПроцент(ТекстНДС);
                
                СтавкаКорректная = ЭтоЧислоСЗапятой(ТекстСтавки);
                
                Если НДСКорректный И СтавкаКорректная Тогда
                    
                    СтрокаТЗ = ТЗПервичка.Добавить();
                    СтрокаТЗ.B_L = ТекстB_L;
                    СтрокаТЗ.Container = ТекстContainer;
                    СтрокаТЗ.CTR_type = ТаблДок.Область("R" + НомерСтроки + "C3").Текст;
                    СтрокаТЗ.Vessel = ТаблДок.Область("R" + НомерСтроки + "C4").Текст;
                    СтрокаТЗ.Рейс = ТаблДок.Область("R" + НомерСтроки + "C5").Текст;
                    СтрокаТЗ.POL = ТаблДок.Область("R" + НомерСтроки + "C6").Текст;
                    СтрокаТЗ.POD = ТаблДок.Область("R" + НомерСтроки + "C7").Текст;
                    СтрокаТЗ.Экспедитор = ТаблДок.Область("R" + НомерСтроки + "C8").Текст;
                    СтрокаТЗ.Договор_экспедитора = ТаблДок.Область("R" + НомерСтроки + "C9").Текст;
                    СтрокаТЗ.Релиз = ТаблДок.Область("R" + НомерСтроки + "C10").Текст;
                    
                    Если ВидУслуги = "Оформление релиз-ордера" Тогда 
                        ТекстСтавкиОчищенный = СтрЗаменить(ТекстСтавки, " ", "");   
                        ТекстСтавкиОчищенный = СтрЗаменить(ТекстСтавкиОчищенный, Символы.НПП, "");
                        ТекстСтавкиОчищенный = СтрЗаменить(ТекстСтавкиОчищенный, ",", ".");
                        СтрокаТЗ.Ставка_релиз_ордер = ТекстСтавкиОчищенный;
                    Иначе
                        ТекстСтавкиОчищенный = СтрЗаменить(ТекстСтавки, " ", "");
                        ТекстСтавкиОчищенный = СтрЗаменить(ТекстСтавкиОчищенный, Символы.НПП, "");
                        ТекстСтавкиОчищенный = СтрЗаменить(ТекстСтавкиОчищенный, ",", ".");
                        СтрокаТЗ.Ставка_док_сбор = ТекстСтавкиОчищенный;
                    КонецЕсли;
                    
                    ТекстНДСОчищенный = СтрЗаменить(ТекстНДС, "%", "");
                    ТекстНДСОчищенный = СтрЗаменить(ТекстНДСОчищенный, " ", "");
                    СтрокаТЗ.НДС = ТекстНДСОчищенный;
                    
                    СчетчикДобавленных = СчетчикДобавленных + 1;
                    
                Иначе
                    СчетчикПропущенных = СчетчикПропущенных + 1;
                КонецЕсли;
                
                НомерСтроки = НомерСтроки + 1;
            Исключение
                Прервать;
            КонецПопытки;
        КонецЦикла;
    // ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}

	Иначе 
		// построение запроса к области ячеек табличного документа
		// с помощью объекта встроенного языка ПостроительЗапроса
        ПЗ = Новый ПостроительЗапроса;
        ПЗ.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблДок.Область());
        ПЗ.ЗаполнитьНастройки();
		
		// выполнение запроса (результат записывается в свойство "Результат")
        ПЗ.Выполнить(); 	
		// выгрузка результата построителя запроса в таблицу значений	
        ТЗПервичка = ПЗ.Результат.Выгрузить(); 
	КонецЕсли; 
	
    // {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399
    ТЗСтрокиСчетаВсе.Очистить();
    ТЗКонтрагенты.Очистить();
    // ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}

    ТЗПлательщики = Новый ТаблицаЗначений();
    ТЗПлательщики.Колонки.Добавить("Плательщик", Новый ОписаниеТипов("Строка")); 
    ТЗПлательщики.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка"));
    ТЗПлательщики.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
    ТЗПлательщики.Колонки.Добавить("Договор", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
    
    ТЗГруппировки = Новый ТаблицаЗначений();
    ТЗГруппировки.Колонки.Добавить("Плательщик", Новый ОписаниеТипов("Строка"));   
    ТЗГруппировки.Колонки.Добавить("Договор", Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов"));
	
	// {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399
    Если ЭтоРелизОрдер ИЛИ ЭтоДокСбор Тогда
        ТЗГруппировки.Колонки.Добавить("B_L", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("Container", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("CTR_type", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("Vessel", Новый ОписаниеТипов("Строка"));  
        ТЗГруппировки.Колонки.Добавить("Рейс", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("POL", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("POD", Новый ОписаниеТипов("Строка"));  
        ТЗГруппировки.Колонки.Добавить("Экспедитор", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("Договор_экспедитора", Новый ОписаниеТипов("Строка")); 
        ТЗГруппировки.Колонки.Добавить("Релиз", Новый ОписаниеТипов("Строка"));  
        Если ЭтоРелизОрдер Тогда
            ТЗГруппировки.Колонки.Добавить("Ставка_релиз_ордер", Новый ОписаниеТипов("Строка"));
        Иначе
            ТЗГруппировки.Колонки.Добавить("Ставка_док_сбор", Новый ОписаниеТипов("Строка"));
        КонецЕсли;
        ТЗГруппировки.Колонки.Добавить("НДС", Новый ОписаниеТипов("Строка"));
	// ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}
    Иначе
        ТЗГруппировки.Колонки.Добавить("Рейс", Новый ОписаниеТипов("Строка")); 
        ТЗГруппировки.Колонки.Добавить("Порт1", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("Порт2", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("ТипКонтейнера", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("СтавкаФрахта", Новый ОписаниеТипов("Строка"));
        ТЗГруппировки.Колонки.Добавить("Terms", Новый ОписаниеТипов("Строка"));
    КонецЕсли;
    
    ТЗГруппировки.Колонки.Добавить("Строки", Новый ОписаниеТипов("Массив"));    
    ТЗГруппировки.Колонки.Добавить("СтрокБольше10", Новый ОписаниеТипов("Число"));
    
    
    Для каждого СтрокаПервички Из ТЗПервичка Цикл
        // {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399
        Если ЭтоРелизОрдер ИЛИ ЭтоДокСбор Тогда            
            Если ПустаяСтрока(СтрокаПервички.B_L) Тогда
                Продолжить;
			КонецЕсли;     
			
            ДанныеПлательщика = ИзвлечьПлательщикаИИНН(СтрокаПервички.Экспедитор);
            Плательщик = ДанныеПлательщика.Плательщик; 
            ИННплательщика = ДанныеПлательщика.ИНН;  
			
            Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("ИНН", ИННплательщика);
			
			Если Контрагент = Неопределено Или Контрагент = Справочники.Контрагенты.ПустаяСсылка() Тогда
                Контрагент = СоздатьКонтрагентаИзПлательщика(Плательщик, ИННплательщика);
            КонецЕсли;
			
            ДоговорСсылка = ПолучитьДоговор(Контрагент, СтрокаПервички.Договор_экспедитора);
            
            Отбор = Новый Структура("Плательщик", Плательщик);
            Отбор.Вставить("Договор", ДоговорСсылка);
            
            СтрокиПлательщику = ТЗПлательщики.НайтиСтроки(Отбор);
            Если СтрокиПлательщику.Количество() = 0 Тогда
                НоваяСтрока = ТЗПлательщики.Добавить();
                НоваяСтрока.Плательщик = Плательщик;        
				НоваяСтрока.ИНН = ИННплательщика;          
                НоваяСтрока.Договор = ДоговорСсылка;
                НоваяСтрока.Контрагент = Контрагент;       
            КонецЕсли;
			
            Отбор = Новый Структура();
            Отбор.Вставить("Плательщик", Плательщик);
            Отбор.Вставить("Договор", ДоговорСсылка);
            Отбор.Вставить("B_L", СтрокаПервички.B_L);
            Отбор.Вставить("Container", СтрокаПервички.Container);
			Отбор.Вставить("CTR_type", СтрокаПервички.CTR_type);
            Отбор.Вставить("Vessel", СтрокаПервички.Vessel);
            Отбор.Вставить("Рейс", СтрокаПервички.Рейс);
            Отбор.Вставить("POL", СтрокаПервички.POL);
            Отбор.Вставить("POD", СтрокаПервички.POD);
            Отбор.Вставить("Экспедитор", СтрокаПервички.Экспедитор);
            Отбор.Вставить("Договор_экспедитора", СтрокаПервички.Договор_экспедитора);
            Отбор.Вставить("Релиз", СтрокаПервички.Релиз);
            Если ЭтоРелизОрдер Тогда
                Отбор.Вставить("Ставка_релиз_ордер", СтрокаПервички.Ставка_релиз_ордер);
            Иначе
                Отбор.Вставить("Ставка_док_сбор", СтрокаПервички.Ставка_док_сбор);
            КонецЕсли;
            Отбор.Вставить("НДС", СтрокаПервички.НДС);
			// ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}
			
        Иначе
            Если ПустаяСтрока(СтрокаПервички.Плательщик) Тогда
                Продолжить;
            КонецЕсли;     
            
            Плательщик = СтрокаПервички.Плательщик;
            
            Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("ИНН", СтрокаПервички.ИНН);
            Если Контрагент = Неопределено Или Контрагент = Справочники.Контрагенты.ПустаяСсылка() Тогда
                Контрагент = СоздатьКонтрагентаИзПлательщика(Плательщик, СтрокаПервички.ИНН);
            КонецЕсли;
            
            ДоговорСсылка = ПолучитьДоговор(Контрагент, СтрокаПервички._Договора_Основание);
            
            Отбор = Новый Структура("Плательщик", Плательщик);
            Отбор.Вставить("Договор", ДоговорСсылка);
            
            СтрокиПлательщику = ТЗПлательщики.НайтиСтроки(Отбор);
            Если СтрокиПлательщику.Количество() = 0 Тогда
                НоваяСтрока = ТЗПлательщики.Добавить();
                НоваяСтрока.Плательщик = Плательщик;
                НоваяСтрока.ИНН = СтрокаПервички.ИНН;   
                НоваяСтрока.Договор = ДоговорСсылка;
                НоваяСтрока.Контрагент = Контрагент;
            КонецЕсли;
            
            Отбор.Вставить("Рейс", СтрокаПервички.Voyage);
            Отбор.Вставить("Порт1", СтрокаПервички.POL);
            Отбор.Вставить("Порт2", СтрокаПервички.POD);
            Отбор.Вставить("ТипКонтейнера", СтрокаПервички.CTRType);
            Отбор.Вставить("СтавкаФрахта", СтрокаПервички.СтавкаФрахта);
            Отбор.Вставить("Terms", СтрокаПервички.Terms); 
        КонецЕсли;
        
        СтрокиПоГруппировке = ТЗГруппировки.НайтиСтроки(Отбор);
        Если СтрокиПоГруппировке.Количество() > 0 Тогда
            СтрокиПоГруппировке[0].Строки.Добавить(СтрокаПервички); 
            СтрокиПоГруппировке[0].СтрокБольше10 = ?(СтрокиПоГруппировке[0].Строки.Количество() > 10, 1, 0);
        Иначе
            НоваяСтрока = ТЗГруппировки.Добавить();
            ЗаполнитьЗначенияСвойств(НоваяСтрока, Отбор);
            
            Если ЭтоРелизОрдер ИЛИ ЭтоДокСбор Тогда
                НоваяСтрока.Плательщик = Плательщик;
            КонецЕсли;
            
            МассивСтрок = Новый Массив();
            МассивСтрок.Добавить(СтрокаПервички);
            НоваяСтрока.Строки = МассивСтрок;  
            НоваяСтрока.СтрокБольше10 = 0;
        КонецЕсли;
    КонецЦикла; 
    
    ТЗПлательщики.Сортировать("Плательщик");
    ТЗКонтрагенты.Загрузить(ТЗПлательщики); 
    
    ТЗГруппировки.Сортировать("Плательщик");    
    
    КурсВалюты = ПолучитьКурсВалюты(Валюта, ДатаКурса);     
    КурсЮань = ПолучитьКурсВалюты(Справочники.Валюты.НайтиПоНаименованию("CNY"), ДатаКурса);
 
    ПлательщикиКолвоСтрок = ТЗГруппировки.Скопировать(,"Плательщик,СтрокБольше10");
    ПлательщикиКолвоСтрок.Свернуть("Плательщик", "СтрокБольше10");    
    
    ТЗДляЗаполнения = Новый ТаблицаЗначений;
    ТЗДляЗаполнения.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка"));
    ТЗДляЗаполнения.Колонки.Добавить("Плательщик", Новый ОписаниеТипов("Строка"));
    ТЗДляЗаполнения.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
    ТЗДляЗаполнения.Колонки.Добавить("Товар", Новый ОписаниеТипов("Строка"));
    ТЗДляЗаполнения.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
    ТЗДляЗаполнения.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число"));
    ТЗДляЗаполнения.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
    ТЗДляЗаполнения.Колонки.Добавить("НДС", Новый ОписаниеТипов("Число"));
    
    ТекПлательщик = "";    
    НомерСтроки = 0;

    Для каждого СтрокаГруппировки Из ТЗГруппировки Цикл
        ПерваяСтрока = СтрокаГруппировки.Строки[0];  
        
        ПлательщикТекущий = СтрокаГруппировки.Плательщик;
        
        Если ТекПлательщик = ПлательщикТекущий Тогда
            НомерСтроки = НомерСтроки + 1;
        Иначе
            НомерСтроки = 1;       
        КонецЕсли;  
        
        Если ТекПлательщик <> ПлательщикТекущий Тогда
            ТекПлательщик = ПлательщикТекущий;
        КонецЕсли;  
		
		// {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399
        Если ПлательщикиКолвоСтрок.Найти(ТекПлательщик, "Плательщик").СтрокБольше10 Тогда
            Букинг = "согласно реестру";
        Иначе
            Если ЭтоРелизОрдер ИЛИ ЭтоДокСбор Тогда
                Букинг = СобратьСтрокуБукингаНовыеУслуги(СтрокаГруппировки.Строки);
            Иначе
                Букинг = СобратьСтрокуБукинга(СтрокаГруппировки.Строки);
            КонецЕсли;
		КонецЕсли; 
		// ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}

        
        СтрокаТФ = ТЗДляЗаполнения.Добавить();
        
        СтрокаТФ.Плательщик = ПлательщикТекущий;
        СтрокаТФ.НомерСтроки = НомерСтроки;
        СтрокаТФ.Товар = ТоварНаименование(ПерваяСтрока, СтрокаГруппировки.Строки.Количество(), Букинг, КурсВалюты, ЭтоУслугаИмпорт, ЭтоРелизОрдер, ЭтоДокСбор);
        
        Если ЭтоРелизОрдер Тогда
            СтрокаТФ.Количество = СтрокаГруппировки.Строки.Количество();
            СтрокаТФ.Цена = БезопасноеЧисло(ПерваяСтрока.Ставка_релиз_ордер) * КурсВалюты.Курс;
            ДанныеПлательщика = ИзвлечьПлательщикаИИНН(ПерваяСтрока.Экспедитор);
            СтрокаТФ.ИНН = ДанныеПлательщика.ИНН;
        ИначеЕсли ЭтоДокСбор Тогда
            СтрокаТФ.Количество = СтрокаГруппировки.Строки.Количество();
            СтрокаТФ.Цена = БезопасноеЧисло(ПерваяСтрока.Ставка_док_сбор) * КурсВалюты.Курс; // Исправлено КурС на Курс
            ДанныеПлательщика = ИзвлечьПлательщикаИИНН(ПерваяСтрока.Экспедитор);
            СтрокаТФ.ИНН = ДанныеПлательщика.ИНН;
        Иначе
            СтрокаТФ.Количество = СтрокаГруппировки.Строки.Количество();
            СтрокаТФ.Цена = Число(ПерваяСтрока.СтавкаФрахта) * КурсВалюты.Курс;
            СтрокаТФ.ИНН = ПерваяСтрока.ИНН;
        КонецЕсли;
        
        СтрокаТФ.Сумма = СтрокаТФ.Цена * СтрокаТФ.Количество; 
        
        Если ЭтоРелизОрдер ИЛИ ЭтоДокСбор Тогда
            ТекстНДС = СтрЗаменить(ПерваяСтрока.НДС, "%", "");
            ТекстНДС = СтрЗаменить(ТекстНДС, " ", "");
            СтрокаТФ.НДС = БезопасноеЧисло(ТекстНДС);
        Иначе
            ТекстНДС = СтрЗаменить(ПерваяСтрока.НДС, "%", "");
            ТекстНДС = СтрЗаменить(ТекстНДС, " ", "");
            СтрокаТФ.НДС = БезопасноеЧисло(ТекстНДС);  
        КонецЕсли;
        
	КонецЦикла;   
	
    // {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399
    ТЗДляЗаполнения.Сортировать("НомерСтроки");
    
    ТЗСтрокиСчетаВсе.Загрузить(ТЗДляЗаполнения);
    
    Если ТЗКонтрагенты.Количество() > 0 Тогда
        ПоказатьСтрокиСчетаПоПлательщику(ТЗКонтрагенты[0].Плательщик);
	КонецЕсли;
	// ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}


КонецПроцедуры

#КонецОбласти



#Область ОбработкаДанных

// {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 
&НаСервере
Функция ИзвлечьПлательщикаИИНН(Знач ТекстЭкспедитора)
    
    Результат = Новый Структура("Плательщик, ИНН");
    Результат.Плательщик = "";
    Результат.ИНН = "";
    
    Если ПустаяСтрока(ТекстЭкспедитора) Тогда
        Возврат Результат;
    КонецЕсли;
    
    ТекстЭкспедитора = СокрЛП(ТекстЭкспедитора);
    ТекстЭкспедитора = СтрЗаменить(ТекстЭкспедитора, Символы.ПС, "");
    ТекстЭкспедитора = СтрЗаменить(ТекстЭкспедитора, Символы.ВК, "");
    
    Если СтрДлина(ТекстЭкспедитора) >= 12 Тогда
        ВозможныйИНН = Прав(ТекстЭкспедитора, 12);
        Если СтрДлина(СтрЗаменить(ВозможныйИНН, " ", "")) = 12 И ЭтоЧисло(ВозможныйИНН) Тогда
            Результат.ИНН = ВозможныйИНН;
            Результат.Плательщик = СокрЛП(Лев(ТекстЭкспедитора, СтрДлина(ТекстЭкспедитора) - 12));
        КонецЕсли;
    КонецЕсли;
    
    Если ПустаяСтрока(Результат.ИНН) И СтрДлина(ТекстЭкспедитора) >= 10 Тогда
        ВозможныйИНН = Прав(ТекстЭкспедитора, 10);
        Если СтрДлина(СтрЗаменить(ВозможныйИНН, " ", "")) = 10 И ЭтоЧисло(ВозможныйИНН) Тогда
            Результат.ИНН = ВозможныйИНН;
            Результат.Плательщик = СокрЛП(Лев(ТекстЭкспедитора, СтрДлина(ТекстЭкспедитора) - 10));
        КонецЕсли;
    КонецЕсли;
    
    Если ПустаяСтрока(Результат.ИНН) Тогда
        Результат.Плательщик = ТекстЭкспедитора;
    КонецЕсли;
    
    Результат.Плательщик = СтрЗаменить(Результат.Плательщик, "ИНН", "");
    Результат.Плательщик = СокрЛП(Результат.Плательщик);
    Если Прав(Результат.Плательщик, 1) = "," Тогда
        Результат.Плательщик = Лев(Результат.Плательщик, СтрДлина(Результат.Плательщик) - 1);
        Результат.Плательщик = СокрЛП(Результат.Плательщик);
    КонецЕсли;
    
    Возврат Результат;
    
КонецФункции
// ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}

// {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 
&НаСервере
Функция ЭтоЧисло(Знач Текст)
    Попытка
        ЧислоЗначение = Число(Текст);
        Возврат Истина;
    Исключение
        Возврат Ложь;
    КонецПопытки;
КонецФункции
// ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}

// {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399
&НаСервере
Функция ЭтоЧислоПроцент(Знач Текст)
    // Проверяет, что текст содержит % и числовое значение
    Если ПустаяСтрока(Текст) Тогда
        Возврат Ложь;
    КонецЕсли;
    
    Если Найти(Текст, "%") = 0 Тогда
        Возврат Ложь;
    КонецЕсли;
    
    // Убираем % и пробелы, проверяем число
    ТекстБезПроцента = СтрЗаменить(Текст, "%", "");
    ТекстБезПроцента = СтрЗаменить(ТекстБезПроцента, " ", "");
    
    Попытка
        ЧислоЗначение = Число(ТекстБезПроцента);
        Возврат Истина;
    Исключение
        Возврат Ложь;
    КонецПопытки;
КонецФункции
// ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}

// {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 
&НаСервере
Функция ЭтоЧислоСЗапятой(Знач Текст)
    // Проверяет, что текст можно преобразовать в число (с запятой или точкой)
    Если ПустаяСтрока(Текст) Тогда
        Возврат Ложь;
    КонецЕсли;
    
    // Убираем пробелы и невидимые символы
    ТекстОчищенный = СтрЗаменить(Текст, " ", "");
    ТекстОчищенный = СтрЗаменить(ТекстОчищенный, Символы.НПП, "");
    
    // Заменяем запятую на точку для проверки
    ТекстОчищенный = СтрЗаменить(ТекстОчищенный, ",", ".");
    
    Попытка
        ЧислоЗначение = Число(ТекстОчищенный);
        Возврат Истина;
    Исключение
        Возврат Ложь;
    КонецПопытки;
КонецФункции   
// ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}

&НаСервере
Функция СоздатьКонтрагентаИзПлательщика(Знач Плательщик, Знач ИНН)
    
    // Поиск существующего контрагента по наименованию
    Контрагент = Справочники.Контрагенты.НайтиПоНаименованию(Плательщик);
    
    Если Контрагент = Неопределено Или Контрагент = Справочники.Контрагенты.ПустаяСсылка() Тогда
        // Создаем нового контрагента
        Попытка
            НовыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
            НовыйКонтрагент.Наименование = Плательщик;
            Если НЕ ПустаяСтрока(ИНН) Тогда
                НовыйКонтрагент.ИНН = ИНН;
            КонецЕсли;
            НовыйКонтрагент.Записать();
            Возврат НовыйКонтрагент.Ссылка;
        Исключение
            // В случае ошибки возвращаем пустую ссылку
            Возврат Справочники.Контрагенты.ПустаяСсылка();
        КонецПопытки;
    Иначе
        Возврат Контрагент;
    КонецЕсли;
    
КонецФункции

&НаСервере
&НаСервере
Функция ТоварНаименование(ПерваяСтрока, КолвоКонтейнеров, Букинг, КурсВалюты, ЭтоУслугаИмпорт, ЭтоРелизОрдер = Ложь, ЭтоДокСбор = Ложь)
  	// {{ ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 
	Если ЭтоРелизОрдер Тогда
        
        СтрокаТХ = СтрШаблон(
            "т/х %1, %2, %3x%4, %5", 
            ПерваяСтрока.Vessel,
            ПерваяСтрока.Рейс, 
            КолвоКонтейнеров,
            ПерваяСтрока.CTR_type,
            Букинг
        ); 
        
        СтрокаСуммыСКурсом = СтрокаСуммыСКурсом(ПерваяСтрока.Ставка_релиз_ордер, КурсВалюты);

        УслугаНаименование = "Услуги по оформлению релиз-ордеров";
        
        Возврат СтрШаблон(
            "%1, 
            |%2.
            |%3",
            УслугаНаименование,
            СтрокаТХ,
            СтрокаСуммыСКурсом
        );      
        
    ИначеЕсли ЭтоДокСбор Тогда
        
        СтрокаСуммыСКурсом = СтрокаСуммыСКурсом(ПерваяСтрока.Ставка_док_сбор, КурсВалюты);

        УслугаНаименование = "Документационный сбор в порту назначения";
        
        Возврат СтрШаблон(
            "%1: %2 т/х %3, %4 %5",
            УслугаНаименование,
            Букинг,
            ПерваяСтрока.Vessel,
            ПерваяСтрока.Рейс,
            СтрокаСуммыСКурсом
        );      
    // ПрофРешение Брикотнин М.А. - 06.11.2025 - #00-3399 }}    
    Иначе
        
        СтрокаТХ = СтрШаблон(
            "т/х %1, %2, %3х%4 %5", 
            ПерваяСтрока.Vessel,
            ПерваяСтрока.Voyage, 
            КолвоКонтейнеров,
            ПерваяСтрока.CTRType,
            ПерваяСтрока.Terms
        ); 
        
        СтрокаСуммыСКурсом = СтрокаСуммыСКурсом(ПерваяСтрока.СтавкаФрахта, КурсВалюты);

        Если ЭтоУслугаИмпорт Тогда
            
            УслугаНаименование = СтрШаблон(
                "Услуги по международной перевозке грузов по маршруту 
                |порт %1, Китай - порт %2, РФ",
                ПерваяСтрока.POD, ПерваяСтрока.POL
            );
        
        Иначе
            
            УслугаНаименование = СтрШаблон(   
                "Услуги по международной перевозке грузов по маршруту согл. дог. %1 по маршруту
                |порт %2, Китай - порт %3, РФ", 
                ПерваяСтрока._Договора_Основание, 
                ПерваяСтрока.POL, ПерваяСтрока.POD
            );
        
        КонецЕсли;
        
        Возврат СтрШаблон(
            "%1, 
            |%2, 
            |%3.
            |%4",
            УслугаНаименование,
            СтрокаТХ,
            Букинг,
            СтрокаСуммыСКурсом
        );      
        
    КонецЕсли;        
    
КонецФункции
&НаСервере
Процедура ДобавитьСтрокуКоносамента(ТЗ, СтрокаСчета, СтрокаКонтейнера, КурсЮань, НомерСтроки)
	
	УслугаКоносамента = СтрШаблон(
		"Услуги по международной перевозке грузов 
		|согл. дог. %1 
		|по маршруту порт %2, Китай - порт %3, РФ,  
		|т/х %4, %5, 
		|к/с %6 локальные сборы в порту %2. 
		|%7",
		СтрокаКонтейнера._Договора_Основание,
		СтрокаКонтейнера.POD, СтрокаКонтейнера.POL,
		СтрокаКонтейнера.Vessel, СтрокаКонтейнера.Voyage, 
		СтрокаКонтейнера.BookingNo_, 
		СтрокаСуммыСКурсом(СтрокаКонтейнера.ЛокальныеСборы_CNY_Trucking, КурсЮань)
	);

	Цена = Число(СтрокаКонтейнера.ЛокальныеСборы_CNY_Trucking) * КурсЮань.Курс;  
	
	СтрКонтейнераБуфер = ТЗ.Найти(Цена, "Цена");
	Если ЗначениеЗаполнено(СтрКонтейнераБуфер) Тогда 
		СтрКонтейнераБуфер.Количество = СтрКонтейнераБуфер.Количество + 1;
	Иначе  
		СтрКонтейнераБуфер = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(СтрКонтейнераБуфер, СтрокаСчета);     
		НомерСтроки = НомерСтроки + 1;
		СтрКонтейнераБуфер.НомерСтроки = НомерСтроки;
		СтрКонтейнераБуфер.Количество  = 1;    
		СтрКонтейнераБуфер.Цена 	   = Цена;
		СтрКонтейнераБуфер.Товар 	   = УслугаКоносамента;  
	КонецЕсли;        
	СтрКонтейнераБуфер.Сумма = СтрКонтейнераБуфер.Количество * СтрКонтейнераБуфер.Цена;
	
КонецПроцедуры  

&НаСервере
Функция БезопасноеЧисло(Знач Значение)

    Если Значение = Неопределено Или ПустаяСтрока(Значение) Тогда
        Возврат 0;
    КонецЕсли;
    
    Попытка
        Текст = СтрЗаменить(Значение, " ", "");
        Текст = СтрЗаменить(Текст, Символы.НПП, "");
        Текст = СтрЗаменить(Текст, ",", ".");
        
        Возврат Число(Текст);
        
    Исключение
        Сообщить("Ошибка преобразования в число: '" + Значение + "'");
        Возврат 0;    
    КонецПопытки;
    
КонецФункции

&НаСервереБезКонтекста
Функция СтрокаСуммыСКурсом(Ставка, КурсВалюты) 
	
	Возврат СтрШаблон(   
		"Ставка %1 %2 по курсу на %3 - %4.",   
		Ставка, 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КурсВалюты.Валюта, "НаименованиеПолное"),
		Формат(КурсВалюты.ДатаКурса, "ДФ=dd.MM.yyyy"),
		КурсВалюты.Курс
	);
	
КонецФункции
#КонецОбласти 


#Область Прочее
&НаСервереБезКонтекста
Функция ПолучитьДоговор(Контрагент, НомерДоговора) 
	
	Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(НомерДоговора) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Владелец", Контрагент);
		Запрос.УстановитьПараметр("Номер", НомерДоговора);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Владелец = &Владелец
		|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
		|	И ДоговорыКонтрагентов.Номер = &Номер";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Договор = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Договор;
	
КонецФункции

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ПоказатьСтрокиСчетаПоПлательщику(Плательщик, Договор = Неопределено)
	
	ТЗСтрокиСчета.Очистить();
	Отбор = Новый Структура();
	Отбор.Вставить("Плательщик", Плательщик); 
	Если Договор <> Неопределено Тогда
	 	Отбор.Вставить("Договор", Договор);
	КонецЕсли;
	ТЗ = РеквизитФормыВЗначение("ТЗСтрокиСчетаВсе", Тип("ТаблицаЗначений"));
	ЗначениеВДанныеФормы(ТЗ.Скопировать(Отбор), ТЗСтрокиСчета);	

КонецПроцедуры 

//собрать по полям BookingNo_ и Container
&НаСервере
Функция СобратьСтрокуБукинга(Строки)
	МассивБукинг = Новый Массив();
	Контейнеры = Новый ТаблицаЗначений();
	Контейнеры.Колонки.Добавить("BookingNo_");
	Контейнеры.Колонки.Добавить("Container");
	Для каждого СтрокаБукинга Из Строки Цикл
		МассивБукинг.Добавить(СтрокаБукинга.BookingNo_);
		НоваяСтрока = Контейнеры.Добавить();
		НоваяСтрока.BookingNo_ = СтрокаБукинга.BookingNo_;
		НоваяСтрока.Container = СтрокаБукинга.Container;
	КонецЦикла;
	
	// собрать строки, разделить точкой с запятой и запятыми
	МассивБукингСтроковый =Новый Массив();
	Для каждого Букинг Из ВернутьУникальные(МассивБукинг) Цикл
		Отбор = Новый Структура();
		Отбор.Вставить("BookingNo_", Букинг);
		КонтейнерыМассив = Новый Массив();
	    Для каждого Строка Из Контейнеры.НайтиСтроки(Отбор) Цикл
			КонтейнерыМассив.Добавить(Строка.Container);
		КонецЦикла;
		
		МассивБукингСтроковый.Добавить(
			СтрШаблон("%1: %2", 
				Букинг,
				СтрСоединить(КонтейнерыМассив, ", ")
			)	
		);
	КонецЦикла;
	Возврат СтрШаблон("к/с %1",
		СтрСоединить(МассивБукингСтроковый, "; ")
	);	
КонецФункции // СобратьСтрокуБукинга()  


&НаСервере
Функция СобратьСтрокуБукингаНовыеУслуги(Строки)

	МассивКонтейнеров = Новый Массив();
	Для каждого СтрокаБукинга Из Строки Цикл
		МассивКонтейнеров.Добавить(СтрокаБукинга.Container);
	КонецЦикла;
	
	УникальныеКонтейнеры = ВернутьУникальные(МассивКонтейнеров);
	
	Возврат СтрСоединить(УникальныеКонтейнеры, ", ");	
КонецФункции



&НаСервере
Функция ВернутьУникальные(МассивНеуникальных)
   Результат = Новый Массив;
   Кэш = Новый Соответствие;
   Для каждого Элемент из МассивНеуникальных Цикл
      Если Кэш[Элемент]=неопределено Тогда
          Кэш[Элемент] = Истина;
          Результат.Добавить(Элемент);
      КонецЕсли;
   КонецЦикла;
   Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКурсВалюты(Валюта, Дата)
	Возврат РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);	
КонецФункции 


&НаКлиенте
Асинх Процедура ВыборФайла(Команда) 
	Отказ = Ложь;
	
	ПроверкаЗаполненияШапки(Отказ);
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ПараметрыДиалогаПомещенияФайлов;
	Диалог.Заголовок = "Выберите файл Excel";
	Диалог.Фильтр = "(*.xls,*.xlsx)|*.xls;*.xlsx;|Microsoft Excel 97/2000/XP/2003 (*.xls)|*.xls|Microsoft Excel 2007/2010 (*.xlsx)|*.xlsx";

	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,Диалог);
	Если ОписаниеФайла <> Неопределено Тогда
	   ЧтениеExcel_через_ТЗ(ОписаниеФайла.Адрес, ОписаниеФайла.СсылкаНаФайл.Расширение);
	КонецЕсли;

	ПоказатьОповещениеПользователя("Обработка файла Excel завершена!");
КонецПроцедуры


&НаКлиенте
Процедура УслугаПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры


&НаСервере
Процедура ВосстановитьПараметрыНаСервере()
	
    КлючОбъекта  = "СчетНаОплатуПокупателю";
    КлючНастроек = "ПараметрыСчетНаОплатуПокупателю";
    СтруктураНастроек = Неопределено; 
	
    Попытка
        СтруктураНастроек = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта,КлючНастроек);
	Исключение
	КонецПопытки;
	
	Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
		
        Услуга = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "Услуга", Справочники.Номенклатура.ПустаяСсылка());
        ЕдиницаИзмерения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "ЕдиницаИзмерения", Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка());
		Валюта = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "Валюта", Справочники.Валюты.ПустаяСсылка());
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "Организация", Справочники.Организации.ПустаяСсылка());
		ДатаКурса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "ДатаКурса", Дата(1,1,1)); 
		ВидУслуги = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "ВидУслуги", "");
		
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Процедура СохранитьПараметрыНаСервере() 
	
    КлючОбъекта  = "СчетНаОплатуПокупателю";
    КлючНастроек = "ПараметрыСчетНаОплатуПокупателю";
	
    Настройки    = Новый Структура("Услуга, ЕдиницаИзмерения, Валюта, Организация, ДатаКурса, ВидУслуги", 
									Услуга, ЕдиницаИзмерения, Валюта, Организация, ДатаКурса, ВидУслуги);
    ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастроек, Настройки);
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВосстановитьПараметрыНаСервере();
	УстановитьДоступностьПолей("Валюта");
КонецПроцедуры

// {{ ПрофРешение Гремицкий А.И. - 24.04.2025 - #00-887 
&НаСервере
Процедура УстановитьДоступностьПолей(ИмяПоля = "")
	
	Если ИмяПоля = "Валюта" Тогда  
		Если ВидУслуги = "Услуги по международной перевозке грузов (экспорт)" Тогда
			Элементы.Валюта.Доступность = Истина;
		Иначе                                 
			Валюта = Справочники.Валюты.НайтиПоНаименованию("USD"); 
        	Элементы.Валюта.Доступность = Ложь;	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
// ПрофРешение Гремицкий А.И. - 24.04.2025 - #00-887 }}

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ТЗКонтрагентыПриАктивизацииСтроки(Элемент)
	Если ТЗКонтрагенты.Количество() > 0 Тогда
		ТекСтрокаПлательщика = Элементы.ТЗКонтрагенты.ТекущиеДанные;
		ПоказатьСтрокиСчетаПоПлательщику(ТекСтрокаПлательщика.Плательщик);  
	Иначе
		ТЗСтрокиСчета.Очистить();	
	КонецЕсли;
КонецПроцедуры


&НаСервере
Функция ЗагрузитьНаСервере(НовыйОбъект, Плательщик, Контрагент, Договор) 
	НовыйОбъект.Дата = ТекущаяДата();
	НовыйОбъект.ДоговорКонтрагента = Договор;
	НовыйОбъект.Контрагент = Контрагент;
	НовыйОбъект.Организация = Организация;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Плательщик", Плательщик);
	Для каждого СтрокаТЗ Из ТЗСтрокиСчетаВсе.НайтиСтроки(Отбор) Цикл
	
		 СтрокаТЧ = НовыйОбъект.Товары.Добавить();
	     СтрокаТЧ.Номенклатура 				= Услуга;
		 СтрокаТЧ.НоменклатураИнтерактивная	= Услуга;
		 СтрокаТЧ.ЭтоУслуга					= Истина;
		 СтрокаТЧ.ЕдиницаИзмерения 			= ЕдиницаИзмерения;
		 СтрокаТЧ.Содержание				= СтрокаТЗ.Товар;
		 СтрокаТЧ.Цена						= СтрокаТЗ.Цена; 
		 СтрокаТЧ.Количество				= СтрокаТЗ.Количество;
		 СтрокаТЧ.Сумма						= СтрокаТЗ.Сумма;
		 СтрокаТЧ.СтавкаНДС					= Перечисления["СтавкиНДС"]["НДС"+СтрокаТЗ.НДС];
		 
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура СоздатьСчет(Команда) 
	
	Отказ = Ложь;
	
	Если ТЗКонтрагенты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ТекСтрокаПлательщика = Элементы.ТЗКонтрагенты.ТекущиеДанные;
	
	ПроверкаЗаполненияШапки(Отказ);
	
	Если НЕ ЗначениеЗаполнено(ТекСтрокаПлательщика.Контрагент) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите контрагента в выбранной строке плательщика");	
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	 
	//Форма = ПолучитьФорму("Документ.СчетНаОплатуПокупателю.ФормаОбъекта"); 
	ИмяОткрываемойФормы = "Документ.СчетНаОплатуПокупателю.ФормаОбъекта";
	Форма = ОткрытьФорму(ИмяОткрываемойФормы, , ЭтотОбъект, Новый УникальныйИдентификатор);
	ДанныеФормы = Форма.Объект;
	ЗагрузитьНаСервере(ДанныеФормы, ТекСтрокаПлательщика.Плательщик, ТекСтрокаПлательщика.Контрагент, ТекСтрокаПлательщика.Договор);
	КопироватьДанныеФормы(ДанныеФормы, Форма.Объект);

	Форма.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаЗаполненияШапки(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Услуга) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите услугу в параметрах", , "Услуга");
		Отказ = Истина;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите единицу измерения в параметрах", , "ЕдиницаИзмерения");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите организацию в параметрах", , "Организация");
		Отказ = Истина;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите валюту в параметрах", , "Валюта");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаКурса) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите дату курса в параметрах", , "ДатаКурса");
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидУслуги) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите вид услуги в параметрах", , "ВидУслуги");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаКурсаПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВидУслугиПриИзменении(Элемент)     
	СохранитьПараметрыНаСервере(); 
	УстановитьДоступностьПолей("Валюта");
КонецПроцедуры

&НаКлиенте
Процедура ВидУслугиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> ВидУслуги И ТЗКонтрагенты.Количество() Тогда
		
		Кнопки = РежимДиалогаВопрос.ДаНет;
		
		Оповещение = Новый ОписаниеОповещения("ВидУслугиОбработкаВыбораЗавершение", ЭтотОбъект, ВидУслуги);
		
		ПоказатьВопрос(Оповещение, "Таблицы будут очищены, продолжить?", Кнопки);
		
	КонецЕсли;	

КонецПроцедуры   

&НаКлиенте
Процедура ВидУслугиОбработкаВыбораЗавершение(РезультатВопроса, СтароеЗначение) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ТЗКонтрагенты.Очистить();
	Иначе
		ВидУслуги = СтароеЗначение;
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти