&НаСервере
 Процедура ЧтениеExcel_через_ТЗ(АдресДанных, РасширениеФайла)

	ПутьКфайлу = ПолучитьИмяВременногоФайла(РасширениеФайла);

	Данные = ПолучитьИзВременногоХранилища(АдресДанных);
	Данные.Записать(ПутьКфайлу);

	ТаблДок = Новый ТабличныйДокумент;

	Попытка   // чтение данных из файла в табличный документ
	   ТаблДок.Прочитать(ПутьКфайлу); 
	Исключение
	   Сообщение = Новый СообщениеПользователю;
	   Сообщение.Текст = "Не удалось прочитать файл Excel, по причине: " + ОписаниеОшибки();
	   Сообщение.Сообщить();
	   Возврат;
	КонецПопытки;

	// построение запроса к области ячеек табличного документа
	// с помощью объекта встроенного языка ПостроительЗапроса
	ПЗ = Новый ПостроительЗапроса;
	ПЗ.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТаблДок.Область());
	ПЗ.ЗаполнитьНастройки();

	// выполнение запроса (результат записывается в свойство "Результат")
	ПЗ.Выполнить();

	// выгрузка результата построителя запроса в таблицу значений
	ТЗПервичка = ПЗ.Результат.Выгрузить(); 
	
	ТЗПлательщики = Новый ТаблицаЗначений();
	ТЗПлательщики.Колонки.Добавить("Плательщик"); 
	ТЗПлательщики.Колонки.Добавить("ИНН");
	ТЗПлательщики.Колонки.Добавить("Контрагент");
	ТЗПлательщики.Колонки.Добавить("Договор");
	
	ТЗГруппировки = Новый ТаблицаЗначений();
	ТЗГруппировки.Колонки.Добавить("Плательщик");   
	ТЗГруппировки.Колонки.Добавить("Договор");
	ТЗГруппировки.Колонки.Добавить("Рейс"); 
	ТЗГруппировки.Колонки.Добавить("Порт1");
	ТЗГруппировки.Колонки.Добавить("Порт2");
	ТЗГруппировки.Колонки.Добавить("ТипКонтейнера");
	ТЗГруппировки.Колонки.Добавить("СтавкаФрахта");
	ТЗГруппировки.Колонки.Добавить("Terms");
	//ТЗГруппировки.Колонки.Добавить("ЛокальныеСборы");
	ТЗГруппировки.Колонки.Добавить("Строки");	
	ТЗГруппировки.Колонки.Добавить("СтрокБольше10");
	
	Для каждого СтрокаПервички Из ТЗПервичка Цикл
		Если ПустаяСтрока(СтрокаПервички.Плательщик) Тогда
        	Продолжить;
		КонецЕсли;     
		
		Отбор = Новый Структура("Плательщик", СтрокаПервички.Плательщик);
		
		Контрагент = Справочники.Контрагенты.НайтиПоРеквизиту("ИНН", СтрокаПервички.ИНН);
		ДоговорСсылка = ПолучитьДоговор(Контрагент, СтрокаПервички._Договора_Основание);
		
		Отбор.Вставить("Договор", ДоговорСсылка);
		
		СтрокиПлательщику = ТЗПлательщики.НайтиСтроки(Отбор);
		Если СтрокиПлательщику.Количество() = 0 Тогда
		    НоваяСтрока = ТЗПлательщики.Добавить();
			НоваяСтрока.Плательщик  	= СтрокаПервички.Плательщик;
			НоваяСтрока.ИНН 		    = СтрокаПервички.ИНН;   
			НоваяСтрока.Договор 		= ДоговорСсылка;
				
			Если НЕ Контрагент = Справочники.Контрагенты.ПустаяСсылка() Тогда
            	НоваяСтрока.Контрагент 	= Контрагент;
			КонецЕсли; 
		КонецЕсли;
		
		Отбор.Договор = СтрокаПервички._Договора_Основание; //отбор теперь не по ссылке, а по имени договора     
		
		Отбор.Вставить("Рейс", СтрокаПервички.Voyage);
		Отбор.Вставить("Порт1", СтрокаПервички.POL);
		Отбор.Вставить("Порт2", СтрокаПервички.POD);
		Отбор.Вставить("ТипКонтейнера", СтрокаПервички.CTRType);
		Отбор.Вставить("СтавкаФрахта", СтрокаПервички.СтавкаФрахта);
		Отбор.Вставить("Terms", СтрокаПервички.Terms); 
		//Отбор.Вставить("ЛокальныеСборы", СтрокаПервички.ЛокальныеСборы_CNY_Trucking);      
		
		СтрокиПоГруппировке = ТЗГруппировки.НайтиСтроки(Отбор);
		Если СтрокиПоГруппировке.Количество() > 0 Тогда
		    СтрокиПоГруппировке[0].Строки.Добавить(СтрокаПервички); 
			СтрокиПоГруппировке[0].СтрокБольше10 = ?(СтрокиПоГруппировке[0].Строки.Количество() > 10, 1, 0);
		Иначе
			НоваяСтрока = ТЗГруппировки.Добавить();
			НоваяСтрока.Плательщик 		= СтрокаПервички.Плательщик;
			НоваяСтрока.Договор 		= СтрокаПервички._Договора_Основание;			
			НоваяСтрока.Рейс 			= СтрокаПервички.Voyage;
			НоваяСтрока.Порт1 			= СтрокаПервички.POL; 
			НоваяСтрока.Порт2 			= СтрокаПервички.POD;
			НоваяСтрока.ТипКонтейнера  	= СтрокаПервички.CTRType;
			НоваяСтрока.СтавкаФрахта  	= СтрокаПервички.СтавкаФрахта; 
			НоваяСтрока.Terms  			= СтрокаПервички.Terms; 
			
			МассивСтрок = Новый Массив();
			МассивСтрок.Добавить(СтрокаПервички);
			НоваяСтрока.Строки 			= МассивСтрок;  
			НоваяСтрока.СтрокБольше10 	= 0;
		КонецЕсли;
	КонецЦикла; 
	
	ТЗКонтрагенты.Очистить(); 
	ТЗПлательщики.Сортировать("Плательщик");
	ТЗКонтрагенты.Загрузить(ТЗПлательщики); 
	
	ТЗГруппировки.Сортировать("Плательщик");	
	
	ТЗСтрокиСчетаВсе.Очистить();    
	
	НомерСтроки = 0;              
	
	ЭтоУслугаИмпорт = (ВидУслуги = "Услуги по международной перевозке грузов (импорт)");
	
	КурсВалюты = ПолучитьКурсВалюты(Валюта, ДатаКурса);     
		
	КурсЮань = ПолучитьКурсВалюты(Справочники.Валюты.НайтиПоНаименованию("CNY"), ДатаКурса);
 
	ПлательщикиКолвоСтрок = ТЗГруппировки.Скопировать(,"Плательщик,СтрокБольше10");
    ПлательщикиКолвоСтрок.Свернуть("Плательщик", "СтрокБольше10");    
	
	ТекПлательщик = "";	
	
	Для каждого СтрокаГруппировки Из ТЗГруппировки Цикл
		ПерваяСтрока = СтрокаГруппировки.Строки[0];  
		
		Если ТекПлательщик = ПерваяСтрока.Плательщик Тогда
			НомерСтроки = НомерСтроки + 1;
		Иначе
			НомерСтроки = 1;		
		КонецЕсли;  
		
		    Если ТекПлательщик <> ПерваяСтрока.Плательщик Тогда
            ТекПлательщик = ПерваяСтрока.Плательщик;
		КонецЕсли;  
		
		Если ПлательщикиКолвоСтрок.Найти(ТекПлательщик, "Плательщик").СтрокБольше10 Тогда
			Букинг = "согласно реестру";
		Иначе
			Букинг = СобратьСтрокуБукинга(СтрокаГруппировки.Строки)
		КонецЕсли;    
		
	    СтрокаТФ = ТЗСтрокиСчетаВсе.Добавить();
		СтрокаТФ.ИНН 		 = ПерваяСтрока.ИНН;
		СтрокаТФ.Плательщик  = ПерваяСтрока.Плательщик;
		СтрокаТФ.НомерСтроки = НомерСтроки;
		СтрокаТФ.Товар 		 = ТоварНаименование(ПерваяСтрока, СтрокаГруппировки.Строки.Количество(), Букинг, КурсВалюты, ЭтоУслугаИмпорт);
		СтрокаТФ.Количество  = СтрокаГруппировки.Строки.Количество();
		СтрокаТФ.Цена 		 = Число(ПерваяСтрока.СтавкаФрахта) * КурсВалюты.Курс;
		СтрокаТФ.Сумма 		 = СтрокаТФ.Цена * СтрокаТФ.Количество; 
		СтрокаТФ.НДС 		 = Число(СтрЗаменить(ПерваяСтрока.НДС, "%", ""));  
		
		ТЗСтрокиКоносамента = РеквизитФормыВЗначение("ТЗСтрокиСчетаВсе").СкопироватьКолонки();
		
		Для каждого СтрКонтейнера Из СтрокаГруппировки.Строки Цикл
			
			ЕстьЛокалСборы = НЕ ПустаяСтрока(СтрКонтейнера.ЛокальныеСборы_CNY_Trucking) 
							 И Число(СтрКонтейнера.ЛокальныеСборы_CNY_Trucking) > 0;
			
			Если ЭтоУслугаИмпорт И ЕстьЛокалСборы Тогда
				ДобавитьСтрокуКоносамента(ТЗСтрокиКоносамента, СтрокаТФ, СтрКонтейнера, КурсЮань, НомерСтроки); 	
			КонецЕсли;	
		
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТЗСтрокиКоносамента, ТЗСтрокиСчетаВсе);
	КонецЦикла;
	
	ТЗСтрокиСчетаВсе.Сортировать("НомерСтроки");
	
	ПоказатьСтрокиСчетаПоПлательщику(ТЗКонтрагенты[0].Плательщик);

КонецПроцедуры                   

&НаСервере
Функция ТоварНаименование(ПерваяСтрока, КолвоКонтейнеров, Букинг, КурсВалюты, ЭтоУслугаИмпорт)
	
	СтрокаТХ = СтрШаблон(
		"т/х %1, %2, %3х%4, %5", 
		ПерваяСтрока.Vessel,
		ПерваяСтрока.Voyage, 
		КолвоКонтейнеров,
		ПерваяСтрока.CTRType,
		ПерваяСтрока.Terms,
	); 
	
	СтрокаСуммыСКурсом = СтрокаСуммыСКурсом(ПерваяСтрока.СтавкаФрахта, КурсВалюты);
	
	Если ЭтоУслугаИмпорт Тогда
		
		УслугаНаименование = СтрШаблон(
			"Услуги по международной перевозке грузов по маршруту 
			|порт %1, Китай - порт %2, РФ",
			ПерваяСтрока.POD, ПерваяСтрока.POL
		);
	
	Иначе
		
		УслугаНаименование = СтрШаблон(   
			"Услуги по международной перевозке грузов по маршруту согл. дог. %1 по маршруту
			|порт %2, Китай - порт %3, РФ", 
			ПерваяСтрока._Договора_Основание, 
			ПерваяСтрока.POL, ПерваяСтрока.POD
		);
	
	КонецЕсли;
	
	Возврат СтрШаблон(
		"%1, 
		|%2, 
		|%3.
		|%4",
		УслугаНаименование,
		СтрокаТХ,
		Букинг,
		СтрокаСуммыСКурсом
	);      
	
КонецФункции

&НаСервере
Процедура ДобавитьСтрокуКоносамента(ТЗ, СтрокаСчета, СтрокаКонтейнера, КурсЮань, НомерСтроки)
	
	УслугаКоносамента = СтрШаблон(
		"Услуги по международной перевозке грузов 
		|согл. дог. %1 
		|по маршруту порт %2, Китай - порт %3, РФ,  
		|т/х %4, %5, 
		|к/с %6 локальные сборы в порту %2. 
		|%7",
		СтрокаКонтейнера._Договора_Основание,
		СтрокаКонтейнера.POD, СтрокаКонтейнера.POL,
		СтрокаКонтейнера.Vessel, СтрокаКонтейнера.Voyage, 
		СтрокаКонтейнера.BookingNo_, 
		СтрокаСуммыСКурсом(СтрокаКонтейнера.ЛокальныеСборы_CNY_Trucking, КурсЮань)
	);

	Цена = Число(СтрокаКонтейнера.ЛокальныеСборы_CNY_Trucking) * КурсЮань.Курс;  
	
	СтрКонтейнераБуфер = ТЗ.Найти(Цена, "Цена");
	Если ЗначениеЗаполнено(СтрКонтейнераБуфер) Тогда 
		СтрКонтейнераБуфер.Количество = СтрКонтейнераБуфер.Количество + 1;
	Иначе  
		СтрКонтейнераБуфер = ТЗ.Добавить();
		ЗаполнитьЗначенияСвойств(СтрКонтейнераБуфер, СтрокаСчета);     
		НомерСтроки = НомерСтроки + 1;
		СтрКонтейнераБуфер.НомерСтроки = НомерСтроки;
		СтрКонтейнераБуфер.Количество  = 1;    
		СтрКонтейнераБуфер.Цена 	   = Цена;
		СтрКонтейнераБуфер.Товар 	   = УслугаКоносамента;  
	КонецЕсли;        
	СтрКонтейнераБуфер.Сумма = СтрКонтейнераБуфер.Количество * СтрКонтейнераБуфер.Цена;
	
КонецПроцедуры  

&НаСервереБезКонтекста
Функция СтрокаСуммыСКурсом(Ставка, КурсВалюты) 
	
	Возврат СтрШаблон(   
		"Ставка %1 %2 по курсу на %3 - %4.",   
		Ставка, 
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КурсВалюты.Валюта, "НаименованиеПолное"),
		Формат(КурсВалюты.ДатаКурса, "ДФ=dd.MM.yyyy"),
		КурсВалюты.Курс
	);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДоговор(Контрагент, НомерДоговора) 
	
	Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(Контрагент) И ЗначениеЗаполнено(НомерДоговора) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Владелец", Контрагент);
		Запрос.УстановитьПараметр("Номер", НомерДоговора);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Владелец = &Владелец
		|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
		|	И ДоговорыКонтрагентов.Номер = &Номер";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Договор = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Договор;
	
КонецФункции

// <Описание процедуры>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
&НаСервере
Процедура ПоказатьСтрокиСчетаПоПлательщику(Плательщик, Договор = Неопределено)
	
	ТЗСтрокиСчета.Очистить();
	Отбор = Новый Структура();
	Отбор.Вставить("Плательщик", Плательщик); 
	Если Договор <> Неопределено Тогда
	 	Отбор.Вставить("Договор", Договор);
	КонецЕсли;
	ТЗ = РеквизитФормыВЗначение("ТЗСтрокиСчетаВсе", Тип("ТаблицаЗначений"));
	ЗначениеВДанныеФормы(ТЗ.Скопировать(Отбор), ТЗСтрокиСчета);	

КонецПроцедуры 

//собрать по полям BookingNo_ и Container
&НаСервере
Функция СобратьСтрокуБукинга(Строки)
	МассивБукинг = Новый Массив();
	Контейнеры = Новый ТаблицаЗначений();
	Контейнеры.Колонки.Добавить("BookingNo_");
	Контейнеры.Колонки.Добавить("Container");
	Для каждого СтрокаБукинга Из Строки Цикл
		МассивБукинг.Добавить(СтрокаБукинга.BookingNo_);
		НоваяСтрока = Контейнеры.Добавить();
		НоваяСтрока.BookingNo_ = СтрокаБукинга.BookingNo_;
		НоваяСтрока.Container = СтрокаБукинга.Container;
	КонецЦикла;
	
	// собрать строки, разделить точкой с запятой и запятыми
	МассивБукингСтроковый =Новый Массив();
	Для каждого Букинг Из ВернутьУникальные(МассивБукинг) Цикл
		Отбор = Новый Структура();
		Отбор.Вставить("BookingNo_", Букинг);
		КонтейнерыМассив = Новый Массив();
	    Для каждого Строка Из Контейнеры.НайтиСтроки(Отбор) Цикл
			КонтейнерыМассив.Добавить(Строка.Container);
		КонецЦикла;
		
		МассивБукингСтроковый.Добавить(
			СтрШаблон("%1: %2", 
				Букинг,
				СтрСоединить(КонтейнерыМассив, ", ")
			)	
		);
	КонецЦикла;
	Возврат СтрШаблон("к/с %1",
		СтрСоединить(МассивБукингСтроковый, "; ")
	);	
КонецФункции // СобратьСтрокуБукинга()


&НаСервере
Функция ВернутьУникальные(МассивНеуникальных)
   Результат = Новый Массив;
   Кэш = Новый Соответствие;
   Для каждого Элемент из МассивНеуникальных Цикл
      Если Кэш[Элемент]=неопределено Тогда
          Кэш[Элемент] = Истина;
          Результат.Добавить(Элемент);
      КонецЕсли;
   КонецЦикла;
   Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКурсВалюты(Валюта, Дата)
	Возврат РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);	
КонецФункции 


&НаКлиенте
Асинх Процедура ВыборФайла(Команда) 
	Отказ = Ложь;
	
	ПроверкаЗаполненияШапки(Отказ);
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ПараметрыДиалогаПомещенияФайлов;
	Диалог.Заголовок = "Выберите файл Excel";
	Диалог.Фильтр = "(*.xls,*.xlsx)|*.xls;*.xlsx;|Microsoft Excel 97/2000/XP/2003 (*.xls)|*.xls|Microsoft Excel 2007/2010 (*.xlsx)|*.xlsx";

	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,Диалог);
	Если ОписаниеФайла <> Неопределено Тогда
	   ЧтениеExcel_через_ТЗ(ОписаниеФайла.Адрес, ОписаниеФайла.СсылкаНаФайл.Расширение);
	КонецЕсли;

	ПоказатьОповещениеПользователя("Обработка файла Excel завершена!");
КонецПроцедуры


&НаКлиенте
Процедура УслугаПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры


&НаСервере
Процедура ВосстановитьПараметрыНаСервере()
	
    КлючОбъекта  = "СчетНаОплатуПокупателю";
    КлючНастроек = "ПараметрыСчетНаОплатуПокупателю";
    СтруктураНастроек = Неопределено; 
	
    Попытка
        СтруктураНастроек = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта,КлючНастроек);
	Исключение
	КонецПопытки;
	
	Если ТипЗнч(СтруктураНастроек) = Тип("Структура") Тогда
		
        Услуга = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "Услуга", Справочники.Номенклатура.ПустаяСсылка());
        ЕдиницаИзмерения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "ЕдиницаИзмерения", Справочники.КлассификаторЕдиницИзмерения.ПустаяСсылка());
		Валюта = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "Валюта", Справочники.Валюты.ПустаяСсылка());
		Организация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "Организация", Справочники.Организации.ПустаяСсылка());
		ДатаКурса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "ДатаКурса", Дата(1,1,1)); 
		ВидУслуги = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураНастроек, "ВидУслуги", "");
		
	КонецЕсли;
	
КонецПроцедуры  

&НаСервере
Процедура СохранитьПараметрыНаСервере() 
	
    КлючОбъекта  = "СчетНаОплатуПокупателю";
    КлючНастроек = "ПараметрыСчетНаОплатуПокупателю";
	
    Настройки    = Новый Структура("Услуга, ЕдиницаИзмерения, Валюта, Организация, ДатаКурса, ВидУслуги", 
									Услуга, ЕдиницаИзмерения, Валюта, Организация, ДатаКурса, ВидУслуги);
    ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастроек, Настройки);
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВосстановитьПараметрыНаСервере();
	УстановитьДоступностьПолей("Валюта");
КонецПроцедуры

// {{ ПрофРешение Гремицкий А.И. - 24.04.2025 - #00-887 
&НаСервере
Процедура УстановитьДоступностьПолей(ИмяПоля = "")
	
	Если ИмяПоля = "Валюта" Тогда  
		Если ВидУслуги = "Услуги по международной перевозке грузов (экспорт)" Тогда
			Элементы.Валюта.Доступность = Истина;
		Иначе                                 
			Валюта = Справочники.Валюты.НайтиПоНаименованию("USD"); 
        	Элементы.Валюта.Доступность = Ложь;	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
// ПрофРешение Гремицкий А.И. - 24.04.2025 - #00-887 }}

&НаКлиенте
Процедура ЕдиницаИзмеренияПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура ТЗКонтрагентыПриАктивизацииСтроки(Элемент)
	Если ТЗКонтрагенты.Количество() > 0 Тогда
		ТекСтрокаПлательщика = Элементы.ТЗКонтрагенты.ТекущиеДанные;
		ПоказатьСтрокиСчетаПоПлательщику(ТекСтрокаПлательщика.Плательщик);  
	Иначе
		ТЗСтрокиСчета.Очистить();	
	КонецЕсли;
КонецПроцедуры


&НаСервере
Функция ЗагрузитьНаСервере(НовыйОбъект, Плательщик, Контрагент, Договор) 
	НовыйОбъект.Дата = ТекущаяДата();
	НовыйОбъект.ДоговорКонтрагента = Договор;
	НовыйОбъект.Контрагент = Контрагент;
	НовыйОбъект.Организация = Организация;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Плательщик", Плательщик);
	Для каждого СтрокаТЗ Из ТЗСтрокиСчетаВсе.НайтиСтроки(Отбор) Цикл
	
		 СтрокаТЧ = НовыйОбъект.Товары.Добавить();
	     СтрокаТЧ.Номенклатура 				= Услуга;
		 СтрокаТЧ.НоменклатураИнтерактивная	= Услуга;
		 СтрокаТЧ.ЭтоУслуга					= Истина;
		 СтрокаТЧ.ЕдиницаИзмерения 			= ЕдиницаИзмерения;
		 СтрокаТЧ.Содержание				= СтрокаТЗ.Товар;
		 СтрокаТЧ.Цена						= СтрокаТЗ.Цена; 
		 СтрокаТЧ.Количество				= СтрокаТЗ.Количество;
		 СтрокаТЧ.Сумма						= СтрокаТЗ.Сумма;
		 СтрокаТЧ.СтавкаНДС					= Перечисления["СтавкиНДС"]["НДС"+СтрокаТЗ.НДС];
		 
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура СоздатьСчет(Команда) 
	
	Отказ = Ложь;
	
	Если ТЗКонтрагенты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ТекСтрокаПлательщика = Элементы.ТЗКонтрагенты.ТекущиеДанные;
	
	ПроверкаЗаполненияШапки(Отказ);
	
	Если НЕ ЗначениеЗаполнено(ТекСтрокаПлательщика.Контрагент) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите контрагента в выбранной строке плательщика");	
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	 
	//Форма = ПолучитьФорму("Документ.СчетНаОплатуПокупателю.ФормаОбъекта"); 
	ИмяОткрываемойФормы = "Документ.СчетНаОплатуПокупателю.ФормаОбъекта";
	Форма = ОткрытьФорму(ИмяОткрываемойФормы, , ЭтотОбъект, Новый УникальныйИдентификатор);
	ДанныеФормы = Форма.Объект;
	ЗагрузитьНаСервере(ДанныеФормы, ТекСтрокаПлательщика.Плательщик, ТекСтрокаПлательщика.Контрагент, ТекСтрокаПлательщика.Договор);
	КопироватьДанныеФормы(ДанныеФормы, Форма.Объект);

	Форма.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаЗаполненияШапки(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Услуга) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите услугу в параметрах", , "Услуга");
		Отказ = Истина;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите единицу измерения в параметрах", , "ЕдиницаИзмерения");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите организацию в параметрах", , "Организация");
		Отказ = Истина;
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Валюта) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите валюту в параметрах", , "Валюта");
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаКурса) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите дату курса в параметрах", , "ДатаКурса");
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидУслуги) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Укажите вид услуги в параметрах", , "ВидУслуги");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаКурсаПриИзменении(Элемент)
	СохранитьПараметрыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВидУслугиПриИзменении(Элемент)     
	СохранитьПараметрыНаСервере(); 
	УстановитьДоступностьПолей("Валюта");
КонецПроцедуры

&НаКлиенте
Процедура ВидУслугиОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> ВидУслуги И ТЗКонтрагенты.Количество() Тогда
		
		Кнопки = РежимДиалогаВопрос.ДаНет;
		
		Оповещение = Новый ОписаниеОповещения("ВидУслугиОбработкаВыбораЗавершение", ЭтотОбъект, ВидУслуги);
		
		ПоказатьВопрос(Оповещение, "Таблицы будут очищены, продолжить?", Кнопки);
		
	КонецЕсли;	

КонецПроцедуры   

&НаКлиенте
Процедура ВидУслугиОбработкаВыбораЗавершение(РезультатВопроса, СтароеЗначение) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ТЗКонтрагенты.Очистить();
	Иначе
		ВидУслуги = СтароеЗначение;
	КонецЕсли; 

КонецПроцедуры